:argv.

SystemClass := !getClassRef "java.lang.System".
File := !getClassRef "java.io.File".
Paths := !getClassRef "java.nio.file.Paths".
Files := !getClassRef "java.nio.file.Files".
MoveOption := !getStaticField "java.nio.file.StandardCopyOption",
                              "REPLACE_EXISTING".
system := system.
metadata := "".

!if ((SystemClass getProperty "os.name") startsWith "Windows"),{
   system := {:cmd.
      !println cmd.
      rc:=!exec "cmd","/c",cmd.
      !println "rc=",rc.
   }.
},{
   system := {:cmd.
      !println cmd.
      rc:=!exec "sh","-c",cmd.
      !println "rc=",rc.
   }.
}.

files := {
   noExt := {:name.
      dot := name lastIndexOf ".".
      !if (dot > 0), {
        name substring 0,dot.
      },{
        name.
      }.
   }.
   getExt := {:name.
      dot := name lastIndexOf ".".
      !if (dot > 0), {
          name substring dot,(name length).
      },{
        name.
      }.
   }.
} new.

tagHelp := {:usage.
   !println ("Usage: joe " + (argv get 0) + " " + (argv get 1) + " " + usage).
}.

fixArgv := {:n.
   newArgv := !newArray (argv length; - n).
   newArgv set 0, (argv get 0).
   idx := 1.
   !foreach argv,(n+1),{:arg.
       newArgv set idx,arg.
       idx := idx + 1.
   }.
   argv := newArgv.
}.

@writeMetadata := {
   ! if (argv length; <= 2), {
      tagHelp exec "<file-name> ... ". 
   },{
      !foreach argv,2,{:file.
         ext := files getExt file.
         tmp := (File createTempFile "ffjoe",ext) getName.
         cmd := "ffmpeg -hide_banner -y -i """+file+""" " + metadata +
                " -acodec copy -vcodec copy """
                     + tmp + """".
         system exec cmd.
         Files move (Paths get tmp,""),(Paths get file,""),MoveOption.
      }.
   }.
}.

setMetadata := {:key,opt.
   tag:=.
   !if (argv length; <= 3 or {tag := !!getVariable (argv get 3). !isNull tag}), {
      tagHelp exec "<value>  nextTag". 
   },{
      metadata := metadata + " -metadata"+opt+key+"="""+(argv get 2)+""" ".
      fixArgv exec 2.
      tag exec.
   }.
}.

@setAlbum := {
   setMetadata exec "album"," ".
}.

@setArtist := {
   setMetadata exec "artist"," ".
}.

@setTitle := {
   setMetadata exec "title"," ".
}.

@split := {
   ! if (argv length; <= 3), {
      tagHelp exec "<file-name> [ <name> <time-start> ] ... ". 
   },{
      ext := files getExt (argv get 2).
      cmd := "ffmpeg -i """+(argv get 2)+""" ".
      idx := 3.
      track := 1.
      start := null.
      !while { (idx + 1) < (argv length) },{
          start := argv get (idx + 1).
          cmd := cmd + " -ss " + start.
          !if ((idx + 3) < (argv length)),{
             start := argv get (idx + 3).
             cmd := cmd + " -to " + start.
          }.
          !if (track < 9),{
             cmd := cmd + " -metadata track=0" + track ;
          },{
             cmd := cmd + " -metadata track=" + track;
          }.
          cmd := cmd + " -metadata title=""" + (argv get idx) + """";
          track := track + 1.
          cmd := cmd + metadata + " -acodec copy -vcodec copy """
                     + (argv get idx)+ext+"""".
          idx := idx + 2.
      }.
      system exec cmd.
   }.
}.

@help := {
   !println ("Usage: joe " + (argv get 0) + " <tag> [ ... ]").
   !println "Available tags:".
   !foreach (!! getVariablesNames), {:tag.
      !if (tag startsWith "@"),{
         !println "   ",tag.
      }.
   }
}.

!if (argv length; > 1),{
   toExec := !!getVariable  (argv get 1).
   !if (!isNull toExec),{
      !println "Tag ",(argv get 1), " is not present!".
      @help exec.
      !systemExit 2.
   },{
      toExec exec.
      !systemExit 0.
   }.
}, {
   @help exec.
}.